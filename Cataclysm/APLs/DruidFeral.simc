actions.precombat+=/mark_of_the_wild,if=!buff.stat_buff.up
#actions.precombat+=/thorns,if=!up
actions.precombat+=/cat_form,if=!up&!buff.bear_form.up
actions.precombat+=/potion

actions+=/use_items
actions+=/potion
actions+=/hyperspeed_acceleration,use_off_gcd=1,if=set_bonus.tier7feral_4pc=1&(buff.tigers_fury.up|cooldown.tigers_fury.remains>=15)
actions+=/run_action_list,name=bearweave,if=buff.bear_form.up&bearweaving_enabled
actions+=/run_action_list,name=bear_tank_aoe,if=buff.bear_form.up&!bearweaving_enabled&active_enemies>4&bear_mode_tank_enabled
actions+=/run_action_list,name=bear_tank,if=buff.bear_form.up&!bearweaving_enabled&bear_mode_tank_enabled
actions+=/run_action_list,name=cat_aoe,if=buff.cat_form.up&active_enemies>2
actions+=/run_action_list,name=cat,if=buff.cat_form.up
actions+=/cat_form,if=!up

# Cat: Default Rotation when in cat_form
actions.cat+=/tigers_fury,use_off_gcd=1,if=try_tigers_fury
actions.cat+=/berserk,use_off_gcd=1,if=try_berserk
actions.cat+=/cancel_buff,buff_name=primal_madness,if=cancel_primal_madness&buff.primal_madness.up&energy.current<20
actions.cat+=/faerie_fire_feral,if=ff_now&target.outside2
actions.cat+=/feral_charge_cat,if=target.outside7
actions.cat+=/mangle_cat,if=feral_t11_refresh_now
actions.cat+=/rip,if=rip_now
actions.cat+=/savage_roar,if=roar_now
actions.cat+=/ferocious_bite,if=bite_now&(energy.current>=action.ferocious_bite.spend|!should_bearweave)
actions.cat+=/mangle_cat,if=mangle_now
actions.cat+=/rake,if=rake_now
actions.cat+=/mangle_cat,if=feral_t11_build_now
actions.cat+=/bear_form,if=should_bearweave
actions.cat+=/feral_charge_cat,if=should_leaveweave
actions.cat+=/ravage,if=ravage_now
actions.cat+=/shred,if=(excess_e>=action.shred.spend|buff.clearcasting.up|buff.berserk.up|energy.current>=energy.max-energy.regen*latency)

# Cat: Default Rotation when in cat_form and multi target
actions.cat_aoe+=/tigers_fury,use_off_gcd=1,if=energy.current<60-(buff.clearcasting.up&15|0)
actions.cat_aoe+=/berserk,if=energy.current>60&cooldown.tigers_fury.remains>=15
actions.cat_aoe+=/savage_roar,if=!up&ttd>1+latency
actions.cat_aoe+=/mangle_cat,if=(set_bonus.idol_of_the_corruptor=1|set_bonus.idol_of_mutilation=1)&combo_points.current=0&buff.savage_roar.remains<=1
actions.cat_aoe+=/shred,if=set_bonus.idol_of_mutilation=1&combo_points.current=0&buff.savage_roar.remains<=1&(ttd>buff.savage_roar.remains+1+latency)
actions.cat_aoe+=/rake,if=!set_bonus.idol_of_mutilation=1&combo_points.current=0&buff.savage_roar.remains<=1&(ttd>buff.savage_roar.remains+1+latency)
actions.cat_aoe+=/swipe_cat,if=excess_e>=action.swipe_cat.spend|buff.clearcasting.up
actions.cat_aoe+=/faerie_fire_feral,if=ff_now

##Bearweave: Use if bearweaving enabled and cat would be downtime
actions.bearweave+=/maul,use_off_gcd=1,if=should_cat&action.maul.ready&!buff.clearcasting.up
actions.bearweave+=/cat_form,if=should_cat
actions.bearweave+=/enrage,use_off_gcd=1,if=!should_cat&rage.current<action.mangle_bear.spend+action.thrash.spend
actions.bearweave+=/mangle_bear
actions.bearweave+=/thrash
actions.bearweave+=/faerie_fire_feral
actions.bearweave+=/maul,use_off_gcd=1,if=!buff.clearcasting.up

##Tankmode: setting enabled and single target
actions.bear_tank+=/frenzied_regeneration,if=health.pct<30
actions.bear_tank+=/survival_instincts,if=health.pct<40
actions.bear_tank+=/feral_charge_bear,if=target.outside7
actions.bear_tank+=/maul,if=rage.current>55
actions.bear_tank+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&dot.lacerate.remains<4
actions.bear_tank+=/faerie_fire_feral,if=settings.maintain_ff&(debuff.major_armor_reduction.down|(debuff.faerie_fire.up&debuff.faerie_fire.remains<6))
actions.bear_tank+=/demoralizing_roar,if=settings.maintain_roar&(debuff.ap_reduction.down|(debuff.demoralizing_roar.up&debuff.demoralizing_roar.remains<4))
actions.bear_tank+=/berserk,if=dot.lacerate.ticking
actions.bear_tank+=/enrage,if=rage.current<80
#actions.bear_tank+=/synapse_springs
actions.bear_tank+=/lacerate,if=!dot.lacerate.ticking&!buff.berserk.up
actions.bear_tank+=/mangle_bear
actions.bear_tank+=/thrash
actions.bear_tank+=/faerie_fire_feral
actions.bear_tank+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&(!buff.pulverize.up|buff.pulverize.remains<4)
actions.bear_tank+=/lacerate,if=dot.lacerate.stack<3
actions.bear_tank+=/maul

#Tankmode: setting enabled and multi target
actions.bear_tank_aoe+=/frenzied_regeneration,if=health.pct<30
actions.bear_tank_aoe+=/survival_instincts,if=health.pct<40
actions.bear_tank_aoe+=/feral_charge_bear,if=target.outside7
actions.bear_tank_aoe+=/maul,if=rage.current>55
actions.bear_tank_aoe+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&dot.lacerate.remains<4
actions.bear_tank_aoe+=/faerie_fire_feral,if=settings.maintain_ff&(debuff.major_armor_reduction.down|(debuff.faerie_fire.up&debuff.faerie_fire.remains<6))
actions.bear_tank_aoe+=/demoralizing_roar,if=settings.maintain_roar&(debuff.ap_reduction.down|(debuff.demoralizing_roar.up&debuff.demoralizing_roar.remains<4))
actions.bear_tank_aoe+=/berserk,if=dot.lacerate.ticking
actions.bear_tank_aoe+=/enrage,if=rage.current<80
#actions.bear_tan_aoek+=/synapse_springs
actions.bear_tank_aoe+=/thrash
actions.bear_tank_aoe+=/swipe_bear,if=active_enemies>3
actions.bear_tank_aoe+=/lacerate,if=!dot.lacerate.ticking&!buff.berserk.up
actions.bear_tank_aoe+=/mangle_bear
actions.bear_tank_aoe+=/faerie_fire_feral
actions.bear_tank_aoe+=/pulverize,if=dot.lacerate.ticking&dot.lacerate.stack=3&(!buff.pulverize.up|buff.pulverize.remains<4)
actions.bear_tank_aoe+=/lacerate,if=dot.lacerate.stack<3
actions.bear_tank_aoe+=/maul
actions.bear_tank_aoe+=/lacerate,cycle_targets=1,if=!dot.lacerate.ticking|dot.lacerate.remains<5