# Opener
actions.precombat+=/soul_harvest,if=soul_shards<3
actions.precombat+=/fel_armor,if=buff.armor.down
actions.precombat+=/volcanic_potion
actions.precombat+=/summon_felhunter,if=!pet.alive

# Default
actions+=/fel_flame,if=buff.fel_spark.up
actions+=/synapse_springs
# Apply Shadow and Flame to the target via Shadow Bolt casting
actions+=/shadow_bolt,if=debuff.shadow_embrace.down & target.health.pct > 25
actions+=/group_curse,if=debuff.my_curse.down & curse_grouped & (debuff.my_curse.id != debuff.curse_of_the_elements.id | talent.jinx.rank & target.debuff.jinx_curse_elements.down)
actions+=/solo_curse,if=debuff.my_curse.down & (debuff.my_curse.id != debuff.curse_of_the_elements.id | talent.jinx.rank & target.debuff.jinx_curse_elements.down)
# Cast Haunt before applying any DoT effect (cast_time + latency + 2 : 2 - average travel time of the cast to a NPC )
actions+=/haunt,use_off_gcd=1,if=!target.debuff.haunt.up|target.dot.haunt.remains<(cast_time+latency+2)
actions+=/demon_soul,if=pet.alive
actions+=/unstable_affliction,if=debuff.unstable_affliction.down
actions+=/run_action_list,name=aoe,if=active_enemies>=2
actions+=/run_action_list,name=single_target,if=active_enemies<2
actions+=/fel_flame,if=moving
# Single Target Rotation
actions.single_target+=/summon_felhunter,if=!pet.alive
# Cast Haunt on cooldown after the first application
actions.single_target+=/haunt,use_off_gcd=1,if=!target.debuff.haunt.up|target.dot.haunt.remains<(cast_time+latency+2)
actions.single_target+=/corruption,if=target.debuff.corruption.remains<tick_time
actions.single_target+=/unstable_affliction,if=target.debuff.unstable_affliction.remains<tick_time&target.health.pct>25
actions.single_target+=/bane,if=!target.debuff.my_bane.ticking
actions.single_target+=/drain_soul,if=target.health.pct<=25
actions.single_target+=/shadowflame,if=target.distance<7
actions.single_target+=/shadow_bolt,if=target.health.pct > 25 & target.distance > 7|debuff.shadow_embrace.down & target.health.pct > 25|target.distance<7&cooldown.shadowflame.remains>3
actions.single_target+=/soulburn,if=moving
actions.single_target+=/soul_fire,if=buff.soulburn.up
actions.single_target+=/life_tap,if=mana.pct<30 | mana.pct<70&moving&glyph.life_tap.enabled
# AoE Rotation
actions.aoe+=/curse_of_the_elements,if=talent.jinx.rank & (target.debuff.jinx_curse_elements.down & debuff.curse_of_the_elements.remains<3)
# Multi-Dotting
# With less than 6 mobs you should multidot all the mobs and keep all debuffs active on them
# Damage over Time Priority
#  1. Corruption
#  2. Unstable Affliction
#  3. Bane of Agony
#  4. Haunt
actions.aoe+=/corruption,if=target.debuff.corruption.remains<2 & active_enemies<6
actions.aoe+=/unstable_affliction,if=target.debuff.unstable_affliction.remains<cast_time
#workaround: something wrong with "bane_priority" evaluation - target.debuff.bane_priority.down is always true.
actions.aoe+=/bane_of_agony,if=target.debuff.bane_of_agony.remains<tick_time &!target.debuff.bane_of_doom.ticking &!target.debuff.bane_of_havoc.ticking
actions.aoe+=/haunt,use_off_gcd=1,if=!target.debuff.haunt.up|target.dot.haunt.remains<(cast_time+latency+2)
# Soul Swap Logic (2 targets)
actions.aoe+=/soul_swap,if=target.debuff.bane_of_agony.up&active_enemies=2
actions.aoe+=/soulburn,if=cooldown.seed_of_corruption.remains<gcd&active_enemies>2
# If there are more than two mobs close together, use  Soulburn  Seed of Corruption to get  Corruption on all the targets. 
# When you hit 6 or more targets, spam  Seed of Corruption on a single target.
actions.aoe+=/seed_of_corruption,if=active_enemies>=6 | buff.soulburn.up & active_enemies>2
actions.aoe+=/shadowflame,if=target.distance<7
actions.aoe+=/shadow_bolt,cycle_targets=1
