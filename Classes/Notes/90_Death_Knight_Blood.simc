# Blood Death Knight
# December 6, 2020

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat=use_item,name=azsharas_font_of_power
actions.precombat+=/use_item,effect_name=cyclotronic_blast

# Executed every time the actor is available.
actions=mind_freeze
actions+=/call_action_list,name=mitigation,if=tanking|incoming_damage_5s>0
actions+=/blood_fury,if=cooldown.dancing_rune_weapon.ready&(!cooldown.blooddrinker.ready|!talent.blooddrinker.enabled)
actions+=/berserking
actions+=/arcane_pulse,if=active_enemies>=2|rune<1&runic_power.deficit>60
actions+=/lights_judgment,if=buff.unholy_strength.up
actions+=/ancestral_call
actions+=/fireblood
actions+=/bag_of_tricks
# Since the potion cooldown has changed, we'll sync with DRW
actions+=/potion,if=buff.dancing_rune_weapon.up
actions+=/use_items
actions+=/raise_dead
actions+=/blooddrinker,if=!buff.dancing_rune_weapon.up&(!covenant.night_fae|buff.deaths_due.remains>7)
actions+=/blood_boil,if=charges>=2&(covenant.kyrian|buff.dancing_rune_weapon.up)
actions+=/raise_dead
actions+=/death_strike,if=fight_remains<3
actions+=/call_action_list,name=covenants
actions+=/call_action_list,name=standard

# Burn RP if we have time between DD refreshes
actions.covenants=death_strike,if=covenant.night_fae&buff.deaths_due.remains>6&runic_power>70
# Make sure we never lose that buff
actions.covenants+=/heart_strike,if=covenant.night_fae&death_and_decay.ticking&((buff.deaths_due.up|buff.dancing_rune_weapon.up)&buff.deaths_due.remains<6)
# And that we always cast DD as high prio when we actually need it
actions.covenants+=/deaths_due,if=!buff.deaths_due.up|buff.deaths_due.remains<4|buff.crimson_scourge.up
# Attempt to sacrifice the ghoul if we predictably will not do much in the near future
actions.covenants+=/sacrificial_pact,if=(!covenant.night_fae|buff.deaths_due.remains>6)&!buff.dancing_rune_weapon.up&(pet.ghoul.remains<10|target.time_to_die<gcd)
# Burn RP off just before swarming comes back off CD
actions.covenants+=/death_strike,if=covenant.venthyr&runic_power>70&cooldown.swarming_mist.remains<3
# And swarming as long as we're not < 3s off DRW
actions.covenants+=/swarming_mist,if=!buff.dancing_rune_weapon.up
# Pre-AL marrow on pull in order to guarantee ossuary during the first DRW
actions.covenants+=/marrowrend,if=covenant.necrolord&buff.bone_shield.stack<=0
# And we cast AL
actions.covenants+=/abomination_limb,if=!buff.dancing_rune_weapon.up
# We just don't cast this during DRW
actions.covenants+=/shackle_the_unworthy,if=cooldown.dancing_rune_weapon.remains<3|!buff.dancing_rune_weapon.up

actions.mitigation=vampiric_blood,if=health.pct<50|(!group&health.pct<66)
# Incoming damage requirement is 25% of max health solo, 50% of max health in a group.
actions.mitigation+=/death_strike,if=incoming_damage_3s>=health.current
# Don't let an existing Blood Shield drop.
actions.mitigation+=/death_strike,if=save_blood_shield&incoming_damage_5s>0&buff.blood_shield.up&buff.blood_shield.remains<1.5*gcd
actions.mitigation+=/marrowrend,if=buff.bone_shield.remains<3*gcd.execute|(buff.bone_shield.remains<=rune.time_to_3|buff.bone_shield.remains<=(gcd+cooldown.blooddrinker.ready*talent.blooddrinker.enabled*2)|buff.bone_shield.stack<3)&runic_power.deficit>=20
actions.mitigation+=/rune_tap,if=tanking&buff.rune_tap.down&incoming_damage_3s>health.max*0.1
actions.mitigation+=/icebound_fortitude,if=health.pct<50-(20*buff.blood_shield.up)
actions.mitigation+=/sacrificial_pact,if=health.pct<75&pet.ghoul.remains<5

actions.standard=death_strike,if=runic_power.deficit<=10&(!talent.bonestorm.enabled|!cooldown.bonestorm.ready|buff.dancing_rune_weapon.up)
actions.standard+=/sacrificial_pact,if=pet.ghoul.remains<gcd*2

# actions.standard+=/blooddrinker,if=!buff.dancing_rune_weapon.up
# Duplicated here because the mitigation list relies on tanking/taking damage.
# actions.standard+=/marrowrend,if=buff.bone_shield.remains<3*gcd.execute|(buff.bone_shield.remains<=rune.time_to_3|buff.bone_shield.remains<=(gcd+cooldown.blooddrinker.ready*talent.blooddrinker.enabled*2)|buff.bone_shield.stack<3)&runic_power.deficit>=20

# Use blood tap to prevent overcapping charges if we have space for a rune and a GCD to spare to burn it
actions.standard=blood_tap,if=rune<=2&rune.time_to_4>gcd&charges_fractional>=1.8
actions.standard+=/dancing_rune_weapon,if=!talent.blooddrinker.enabled|!cooldown.blooddrinker.ready
actions.standard+=/tombstone,if=buff.bone_shield.stack>=7&rune>=2
actions.standard+=/marrowrend,if=(!covenant.necrolord|buff.abomination_limb.up)&(buff.bone_shield.remains<=rune.time_to_3|buff.bone_shield.remains<=(gcd+cooldown.blooddrinker.ready*talent.blooddrinker.enabled*2)|buff.bone_shield.stack<3)&runic_power.deficit>=20
# actions.standard+=/death_strike,if=runic_power.deficit<=70
actions.standard+=/marrowrend,if=buff.bone_shield.stack<6&runic_power.deficit>=15&(!covenant.night_fae|buff.deaths_due.remains>5)
actions.standard+=/heart_strike,if=!talent.blooddrinker.enabled&death_and_decay.remains<5&runic_power.deficit<=(15+buff.dancing_rune_weapon.up*5+spell_targets.heart_strike*talent.heartbreaker.enabled*2)
actions.standard+=/blood_boil,if=charges_fractional>=1.8&(buff.hemostasis.stack<=(5-spell_targets.blood_boil)|spell_targets.blood_boil>2)
# Mark of Blood does not pandemic-extend.
actions.standard+=/mark_of_blood,if=debuff.mark_of_blood.down&fight_remains>7.5
actions.standard+=/death_and_decay,if=(buff.crimson_scourge.up&talent.relish_in_blood.enabled)&runic_power.deficit>10
actions.standard+=/bonestorm,if=runic_power>=100&!buff.dancing_rune_weapon.up
actions.standard+=/death_strike,if=runic_power.deficit<=(15+buff.dancing_rune_weapon.up*5+spell_targets.heart_strike*talent.heartbreaker.enabled*2)|fight_remains<10
actions.standard+=/death_and_decay,if=spell_targets.death_and_decay>=3
actions.standard+=/heart_strike,if=buff.dancing_rune_weapon.up|rune.time_to_4<gcd
actions.standard+=/blood_boil,if=buff.dancing_rune_weapon.up
actions.standard+=/blood_tap,if=rune.time_to_3>gcd
actions.standard+=/death_and_decay,if=buff.crimson_scourge.up|talent.rapid_decomposition.enabled|spell_targets.death_and_decay>=2
actions.standard+=/consumption,if=buff.bone_shield.remains>3*gcd
actions.standard+=/blood_boil,if=charges_fractional>=1.1&buff.bone_shield.remains>3*gcd
actions.standard+=/heart_strike,if=rune.time_to_3<gcd|buff.bone_shield.stack>7&buff.bone_shield.remains>rune.time_to_3+gcd
actions.standard+=/arcane_torrent,if=runic_power.deficit>20