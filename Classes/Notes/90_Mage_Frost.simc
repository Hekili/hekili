## Mage Frost
## November 13, 2021

## Changes:
## - Don't include Blink in Movement APL (positioning is a player decision)
## - Make some 'fight_remains' entries apply to boss fights only.

## Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/arcane_intellect
actions.precombat+=/summon_water_elemental
actions.precombat+=/blizzard,if=active_enemies>=2
actions.precombat+=/frostbolt,if=active_enemies=1

## Executed every time the actor is available.
actions=counterspell,if=!runeforge.disciplinary_command&debuff.casting.up&debuff.casting.remains<1|cooldown.buff_disciplinary_command.ready&buff.disciplinary_command_arcane.down
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=essences
actions+=/call_action_list,name=aoe,strict=1,if=active_enemies>=3
actions+=/call_action_list,name=st,strict=1,if=active_enemies<3
actions+=/call_action_list,name=movement

actions.aoe=frozen_orb
actions.aoe+=/blizzard
actions.aoe+=/flurry,if=(remaining_winters_chill=0|debuff.winters_chill.down)&(prev_gcd.1.ebonbolt|buff.brain_freeze.react&buff.fingers_of_frost.react=0)
actions.aoe+=/ice_nova
actions.aoe+=/comet_storm
actions.aoe+=/ice_lance,if=buff.fingers_of_frost.react|debuff.frozen.remains>travel_time|remaining_winters_chill&debuff.winters_chill.remains>travel_time
actions.aoe+=/radiant_spark,if=soulbind.combat_meditation
actions.aoe+=/mirrors_of_torment
actions.aoe+=/shifting_power
actions.aoe+=/frost_nova,if=runeforge.grisly_icicle&target.level<=level&debuff.frozen.down
actions.aoe+=/fire_blast,if=runeforge.disciplinary_command&cooldown.buff_disciplinary_command.ready&buff.disciplinary_command_fire.down
actions.aoe+=/arcane_explosion,if=mana.pct>30&active_enemies>=6&!runeforge.glacial_fragments
actions.aoe+=/ebonbolt
actions.aoe+=/ice_lance,if=runeforge.glacial_fragments&(talent.splitting_ice|active_enemies>=5)&travel_time<ground_aoe.blizzard.remains
## actions.aoe+=/wait,sec=0.1,if=runeforge.glacial_fragments&talent.splitting_ice
actions.aoe+=/frostbolt

actions.cds=use_item,name=shadowed_orb_of_torment,if=buff.rune_of_power.down
actions.cds+=/potion,if=prev_off_gcd.icy_veins|boss&fight_remains<30
actions.cds+=/deathborne
actions.cds+=/mirrors_of_torment,if=active_enemies<3&(conduit.siphoned_malice|soulbind.wasteland_propriety)&buff.brain_freeze.react=0
actions.cds+=/rune_of_power,if=cooldown.icy_veins.remains>12&buff.rune_of_power.down
actions.cds+=/icy_veins,if=buff.rune_of_power.down&(buff.icy_veins.down|talent.rune_of_power)&(buff.slick_ice.down|active_enemies>=2)
actions.cds+=/time_warp,if=runeforge.temporal_warp&buff.exhaustion.up&(prev_off_gcd.icy_veins|boss&fight_remains<40)
actions.cds+=/use_items
actions.cds+=/blood_fury
actions.cds+=/berserking
actions.cds+=/lights_judgment
actions.cds+=/fireblood
actions.cds+=/ancestral_call
actions.cds+=/bag_of_tricks

## Disabled because Blink/Shimmer is a player choice.
## actions.movement=blink_any,if=movement.distance>10
actions.movement+=/ice_floes,if=buff.ice_floes.down
actions.movement+=/arcane_explosion,if=mana.pct>30&active_enemies>=2
actions.movement+=/fire_blast
actions.movement+=/ice_lance

actions.st=flurry,if=(remaining_winters_chill=0|debuff.winters_chill.down)&(prev_gcd.1.ebonbolt|buff.brain_freeze.react&(prev_gcd.1.glacial_spike|prev_gcd.1.frostbolt&(!conduit.ire_of_the_ascended|cooldown.radiant_spark.remains|runeforge.freezing_winds)|prev_gcd.1.radiant_spark|buff.fingers_of_frost.react=0&(debuff.mirrors_of_torment.up|buff.freezing_winds.up|buff.expanded_potential.react)))
actions.st+=/frozen_orb
actions.st+=/blizzard,if=buff.freezing_rain.up|active_enemies>=2
actions.st+=/ray_of_frost,if=remaining_winters_chill=1&debuff.winters_chill.remains
actions.st+=/glacial_spike,if=remaining_winters_chill&debuff.winters_chill.remains>cast_time+travel_time
actions.st+=/ice_lance,if=remaining_winters_chill&remaining_winters_chill>buff.fingers_of_frost.react&debuff.winters_chill.remains>travel_time
actions.st+=/comet_storm
actions.st+=/ice_nova
actions.st+=/radiant_spark,if=buff.freezing_winds.up&active_enemies=1
actions.st+=/radiant_spark,if=buff.brain_freeze.react&talent.glacial_spike&conduit.ire_of_the_ascended&buff.icicles.stack>=4
actions.st+=/ice_lance,if=buff.fingers_of_frost.react|debuff.frozen.remains>travel_time
actions.st+=/ebonbolt
actions.st+=/radiant_spark,if=(!talent.glacial_spike|!conduit.ire_of_the_ascended)&(!runeforge.freezing_winds|active_enemies>=2)&buff.brain_freeze.react
actions.st+=/mirrors_of_torment
actions.st+=/shifting_power,if=buff.rune_of_power.down&(runeforge.heart_of_the_fae|soulbind.grove_invigoration|soulbind.field_of_blossoms|runeforge.freezing_winds&buff.freezing_winds.down|active_enemies>=2)
actions.st+=/frost_nova,if=runeforge.grisly_icicle&target.level<=level&debuff.frozen.down
actions.st+=/arcane_explosion,if=runeforge.disciplinary_command&cooldown.buff_disciplinary_command.ready&buff.disciplinary_command_arcane.down
actions.st+=/fire_blast,if=runeforge.disciplinary_command&cooldown.buff_disciplinary_command.ready&buff.disciplinary_command_fire.down
actions.st+=/glacial_spike,if=buff.brain_freeze.react
actions.st+=/frostbolt