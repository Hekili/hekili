## Arms Warrior
## February 25, 2022

## Changes:
## - Update for 9.2; add target swapping for Execute/Condemn.

## Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/battle_shout

## Executed every time the actor is available.
actions=charge
actions+=/pummel
actions+=/potion,if=gcd.remains=0&debuff.colossus_smash.remains>8|boss&fight_remains<25
actions+=/blood_fury,if=debuff.colossus_smash.up
actions+=/berserking,if=debuff.colossus_smash.remains>6
actions+=/arcane_torrent,if=cooldown.mortal_strike.remains>1.5&rage<50
actions+=/lights_judgment,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/fireblood,if=debuff.colossus_smash.up
actions+=/ancestral_call,if=debuff.colossus_smash.up
actions+=/bag_of_tricks,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/use_item,name=scars_of_fraternal_strife
actions+=/use_items
actions+=/sweeping_strikes,if=spell_targets.whirlwind>1&(cooldown.bladestorm.remains>15|talent.ravager.enabled)
actions+=/call_action_list,name=execute,strict=1,if=talent.massacre.enabled&target.health.pct<35|target.health.pct<20|covenant.venthyr&target.health.pct>80|cycle_for_execute|cycle_for_condemn
actions+=/call_action_list,name=hac,strict=1,if=raid_event.adds.up|spell_targets.whirlwind>1
actions+=/call_action_list,name=single_target
actions+=/victory_rush
actions+=/impending_victory

actions.execute=deadly_calm
actions.execute+=/conquerors_banner
actions.execute+=/cancel_buff,name=bladestorm,if=spell_targets.whirlwind=1&gcd.remains=0&(rage>75|rage>50&buff.recklessness.up)
actions.execute+=/avatar,if=gcd.remains=0|target.time_to_die<20
actions.execute+=/condemn,cycle_targets=1,if=buff.ashen_juggernaut.up&buff.ashen_juggernaut.remains<gcd&conduit.ashen_juggernaut.rank>1
actions.execute+=/execute,cycle_targets=1,if=buff.ashen_juggernaut.up&buff.ashen_juggernaut.remains<gcd&conduit.ashen_juggernaut.rank>1
actions.execute+=/ravager
actions.execute+=/rend,if=remains<=gcd&(!talent.warbreaker.enabled&cooldown.colossus_smash.remains<4|talent.warbreaker.enabled&cooldown.warbreaker.remains<4)&target.time_to_die>12
actions.execute+=/warbreaker
actions.execute+=/colossus_smash
actions.execute+=/ancient_aftershock
actions.execute+=/spear_of_bastion
actions.execute+=/condemn,cycle_targets=1,if=runeforge.signet_of_tormented_kings&(rage.deficit<25|debuff.colossus_smash.up&rage>40|buff.sudden_death.react|buff.deadly_calm.up)
actions.execute+=/overpower,if=charges=2
actions.execute+=/cleave,if=spell_targets.whirlwind>1&dot.deep_wounds.remains<gcd
actions.execute+=/mortal_strike,if=dot.deep_wounds.remains<=gcd|runeforge.enduring_blow|buff.overpower.stack=2&debuff.exploiter.stack=2|buff.battlelord.up
actions.execute+=/condemn,cycle_targets=1,if=rage.deficit<25|buff.deadly_calm.up
actions.execute+=/skullsplitter,if=rage<45
actions.execute+=/bladestorm,if=buff.deadly_calm.down&(rage<20|!runeforge.sinful_surge&rage<50)
actions.execute+=/overpower
actions.execute+=/condemn,cycle_targets=1
actions.execute+=/execute,cycle_targets=1

actions.hac=skullsplitter,if=rage<60&buff.deadly_calm.down
actions.hac+=/conquerors_banner
actions.hac+=/avatar,if=cooldown.colossus_smash.remains<1
actions.hac+=/warbreaker
actions.hac+=/colossus_smash
actions.hac+=/cleave,if=dot.deep_wounds.remains<=gcd
actions.hac+=/ancient_aftershock
actions.hac+=/spear_of_bastion
actions.hac+=/bladestorm
actions.hac+=/ravager
actions.hac+=/rend,if=remains<=duration*0.3&buff.sweeping_strikes.up
actions.hac+=/cleave
actions.hac+=/mortal_strike,if=buff.sweeping_strikes.up|dot.deep_wounds.remains<gcd&!talent.cleave.enabled
actions.hac+=/overpower,if=talent.dreadnaught.enabled
actions.hac+=/condemn,cycle_targets=1,if=buff.sweeping_strikes.up|buff.sudden_death.up
actions.hac+=/execute,cycle_targets=1,if=buff.sweeping_strikes.up|buff.sudden_death.up
actions.hac+=/overpower
actions.hac+=/whirlwind

actions.single_target=rend,if=remains<=gcd
actions.single_target+=/conquerors_banner,if=fight_remains>140
actions.single_target+=/avatar
actions.single_target+=/ravager
actions.single_target+=/warbreaker
actions.single_target+=/colossus_smash
actions.single_target+=/ancient_aftershock
actions.single_target+=/spear_of_bastion
actions.single_target+=/overpower,if=charges=2
actions.single_target+=/mortal_strike,if=runeforge.enduring_blow|runeforge.battlelord|buff.overpower.stack>=2
actions.single_target+=/condemn,if=buff.sudden_death.react
actions.single_target+=/execute,if=buff.sudden_death.react
actions.single_target+=/skullsplitter,if=rage.deficit>45&buff.deadly_calm.down
actions.single_target+=/bladestorm,if=buff.deadly_calm.down&rage<30
actions.single_target+=/deadly_calm
actions.single_target+=/overpower
actions.single_target+=/mortal_strike
actions.single_target+=/rend,if=remains<duration*0.3
actions.single_target+=/cleave,if=spell_targets.whirlwind>1
actions.single_target+=/whirlwind,if=talent.fervor_of_battle.enabled|spell_targets.whirlwind>4|spell_targets.whirlwind>2&buff.sweeping_strikes.down
actions.single_target+=/slam,if=!talent.fervor_of_battle.enabled&(rage>50|debuff.colossus_smash.up|!runeforge.enduring_blow)