## Arms Warrior
## March 14, 2021

## Changes:
## - Added Pummel.
## - Added Victory Rush/Impending Victory.
## - Added Battle Shout to precombat APL.
## - Executioner's Precision is deleted.
## - Added Use Items.
## - Tweak entry for Bladestorm.

## Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/battle_shout

## Executed every time the actor is available.
actions=charge
actions+=/pummel
actions+=/potion
actions+=/blood_fury,if=debuff.colossus_smash.up
actions+=/berserking,if=debuff.colossus_smash.remains>6
actions+=/arcane_torrent,if=cooldown.mortal_strike.remains>1.5&rage<50
actions+=/lights_judgment,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/fireblood,if=debuff.colossus_smash.up
actions+=/ancestral_call,if=debuff.colossus_smash.up
actions+=/bag_of_tricks,if=debuff.colossus_smash.down&cooldown.mortal_strike.remains
actions+=/use_item,name=inscrutable_quantum_device
actions+=/use_item,name=dreadfire_vessel
actions+=/use_items
actions+=/sweeping_strikes,if=spell_targets.whirlwind>1&(cooldown.bladestorm.remains>15|talent.ravager.enabled)
actions+=/call_action_list,name=execute,strict=1,if=(talent.massacre.enabled&target.health.pct<35)|target.health.pct<20|(target.health.pct>80&covenant.venthyr)
actions+=/call_action_list,name=hac,strict=1,if=spell_targets.whirlwind>1
actions+=/call_action_list,name=single_target
actions+=/victory_rush
actions+=/impending_victory

actions.execute=deadly_calm
actions.execute+=/cancel_buff,name=bladestorm,if=spell_targets.whirlwind=1&gcd.remains=0&(rage>75|rage>50&buff.recklessness.up)
actions.execute+=/avatar,if=cooldown.colossus_smash.remains<8&gcd.remains=0
actions.execute+=/skullsplitter,if=rage<60&(!talent.deadly_calm.enabled|buff.deadly_calm.down)
actions.execute+=/ravager
actions.execute+=/conquerors_banner
actions.execute+=/cleave,if=spell_targets.whirlwind>1&dot.deep_wounds.remains<gcd
actions.execute+=/warbreaker
actions.execute+=/colossus_smash
actions.execute+=/condemn,if=debuff.colossus_smash.up|buff.sudden_death.react|rage>65
actions.execute+=/overpower,if=charges=2
actions.execute+=/ancient_aftershock
actions.execute+=/spear_of_bastion
actions.execute+=/mortal_strike,if=runeforge.enduring_blow|dot.deep_wounds.remains<=gcd|((debuff.exploiter.stack=2|buff.battlelord.up)&!covenant.venthyr)
actions.execute+=/bladestorm,if=buff.deadly_calm.down&rage<50
actions.execute+=/skullsplitter,if=rage<40
actions.execute+=/overpower
actions.execute+=/condemn
actions.execute+=/execute

actions.hac=skullsplitter,if=rage<60&buff.deadly_calm.down
actions.hac+=/conquerors_banner
actions.hac+=/avatar,if=cooldown.colossus_smash.remains<1
actions.hac+=/warbreaker
actions.hac+=/colossus_smash
actions.hac+=/cleave,if=dot.deep_wounds.remains<=gcd
actions.hac+=/ancient_aftershock
actions.hac+=/spear_of_bastion
actions.hac+=/bladestorm
actions.hac+=/ravager
actions.hac+=/rend,if=remains<=duration*0.3&buff.sweeping_strikes.up
actions.hac+=/cleave
actions.hac+=/mortal_strike,if=buff.sweeping_strikes.up|dot.deep_wounds.remains<gcd&!talent.cleave.enabled
actions.hac+=/overpower,if=talent.dreadnaught.enabled
actions.hac+=/condemn
actions.hac+=/execute,if=buff.sweeping_strikes.up
actions.hac+=/overpower
actions.hac+=/whirlwind

actions.single_target=conquerors_banner,if=(fight_remains>180|(target.health.pct<20|talent.massacre.enabled&target.health.pct<35))
actions.single_target=rend,if=remains<=duration*0.3
actions.single_target+=/avatar,if=cooldown.colossus_smash.remains<8&gcd.remains=0
actions.single_target+=/cleave,if=spell_targets.whirlwind>1&dot.deep_wounds.remains<gcd
actions.single_target+=/ravager
actions.single_target+=/warbreaker
actions.single_target+=/colossus_smash
actions.single_target+=/ancient_aftershock
actions.single_target+=/spear_of_bastion
actions.single_target+=/overpower,if=charges=2
actions.single_target+=/mortal_strike,if=runeforge.enduring_blow|buff.overpower.stack>=2|(dot.deep_wounds.remains<=gcd&cooldown.colossus_smash.remains>gcd)
actions.single_target+=/bladestorm,if=buff.deadly_calm.down&(debuff.colossus_smash.up&rage<30|rage<50)
actions.single_target+=/deadly_calm
actions.single_target+=/skullsplitter,if=rage<60&buff.deadly_calm.down
actions.single_target+=/overpower
actions.single_target+=/condemn,if=buff.sudden_death.react
actions.single_target+=/execute,if=buff.sudden_death.react
actions.single_target+=/mortal_strike
actions.single_target+=/whirlwind,if=talent.fervor_of_battle.enabled
actions.single_target+=/slam,if=!talent.fervor_of_battle.enabled&(rage>50|runeforge.signet_of_tormented_kings)
