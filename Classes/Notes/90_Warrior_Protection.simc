## Protection Warrior
## February 8, 2021

## Changes
## - Add Mitigation logic to new SimC APL.
## - Lighten Shield Block and Ignore Pain damage intake requirements.
## - Remove rotational usage of Last Stand.
## - Respect "Free Revenge" setting and remove other restrictions.

actions.precombat+=/battle_shout
actions.precombat+=/potion

## Executed every time the actor is available.
actions=charge
actions+=/pummel
actions+=/shockwave,if=debuff.casting.up&!target.is_boss
actions+=/use_items,if=cooldown.avatar.remains<=gcd|buff.avatar.up
actions+=/blood_fury
actions+=/berserking
actions+=/arcane_torrent
actions+=/lights_judgment
actions+=/fireblood
actions+=/ancestral_call
actions+=/bag_of_tricks
actions+=/potion,if=buff.avatar.up|fight_remains<25
actions+=/ignore_pain,if=rage.deficit<40*talent.booming_voice.enabled*cooldown.demoralizing_shout.ready
actions+=/call_action_list,name=mitigation,if=incoming_damage_5s>0
actions+=/avatar
actions+=/ancient_aftershock
actions+=/spear_of_bastion
actions+=/conquerors_banner
actions+=/shield_block,if=buff.shield_block.down
actions+=/run_action_list,name=aoe,if=spell_targets.thunder_clap>=3
actions+=/call_action_list,name=generic

actions.mitigation=ignore_pain,if=rage>50&buff.ignore_pain.down
actions.mitigation+=/shield_block,if=(!talent.bolster.enabled|!buff.last_stand.up)&(charges_fractional>1.8|incoming_damage_5s)
actions.mitigation+=/demoralizing_shout,if=(target.within10|active_enemies>1)&((incoming_damage_5s>0.15*health.max|health.pct<30)|(!talent.booming_voice.enabled|rage.max-rage<40))
actions.mitigation+=/last_stand,if=incoming_damage_3s>health&cooldown.anima_of_death.remains<=2
actions.mitigation+=/shield_wall,if=incoming_damage_5s>health|health.pct<20

actions.aoe=ravager
actions.aoe+=/dragon_roar
actions.aoe+=/thunder_clap
actions.aoe+=/revenge
actions.aoe+=/shield_slam

actions.generic=ravager
actions.generic+=/dragon_roar
actions.generic+=/shield_slam,if=buff.shield_block.up
actions.generic+=/thunder_clap,if=(spell_targets.thunder_clap>1|cooldown.shield_slam.remains)&talent.unstoppable_force.enabled&buff.avatar.up
actions.generic+=/shield_slam
actions.generic+=/condemn
actions.generic+=/execute
actions.generic+=/revenge,if=rage>=70
actions.generic+=/thunder_clap
actions.generic+=/revenge
actions.generic+=/victory_rush
actions.generic+=/impending_victory
actions.generic+=/devastate