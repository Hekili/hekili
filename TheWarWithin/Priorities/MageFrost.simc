actions.precombat+=/variable,name=time_to_coc,condition=talent.frostfire_bolt.enabled,value=10,value_else=23
actions.precombat+=/arcane_intellect
actions.precombat+=/mirror_image
actions.precombat+=/blizzard,if=active_enemies>=2&talent.ice_caller&!talent.fractured_frost|active_enemies>=4
actions.precombat+=/frostbolt,if=active_enemies<=3

actions+=/counterspell
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=aoe,strict=1,if=active_enemies>=7|active_enemies>=3&!talent.frostfire_bolt|active_enemies>=4
actions+=/call_action_list,name=cleave_ff,strict=1,if=active_enemies>=2&active_enemies<=3&talent.frostfire_bolt
actions+=/call_action_list,name=cleave_ss,strict=1,if=active_enemies>=2
actions+=/call_action_list,name=st_aoebuild,strict=1,if=talent.splinterstorm&!(talent.cold_front&talent.slick_ice&talent.deaths_chill&talent.frozen_touch)|talent.frostfire_bolt&!talent.deep_shatter
actions+=/call_action_list,name=st_ff,strict=1,if=talent.frostfire_bolt
actions+=/call_action_list,name=st_ss


actions.aoe+=/cone_of_cold,if=talent.coldest_snap&(prev_gcd.1.comet_storm|prev_gcd.1.frozen_orb&!talent.comet_storm)
actions.aoe+=/frozen_orb,if=(!prev_gcd.1.cone_of_cold|!talent.isothermic_core)&(!prev_gcd.1.glacial_spike|!freezable)
actions.aoe+=/blizzard,if=!prev_gcd.1.glacial_spike|!freezable
actions.aoe+=/frostbolt,if=buff.icy_veins.up&(buff.deaths_chill.stack<9|buff.deaths_chill.stack=9&!action.frostbolt.in_flight)&buff.icy_veins.remains>8&talent.deaths_chill
actions.aoe+=/comet_storm,if=!prev_gcd.1.glacial_spike&(!talent.coldest_snap|cooldown.cone_of_cold.ready&cooldown.frozen_orb.remains>25|(cooldown.cone_of_cold.remains>10&talent.frostfire_bolt|cooldown.cone_of_cold.remains>20&!talent.frostfire_bolt))
actions.aoe+=/freeze,if=freezable&debuff.frozen.down&(!talent.glacial_spike|prev_gcd.1.glacial_spike)
actions.aoe+=/ice_nova,if=freezable&!prev.1.freeze&(prev_gcd.1.glacial_spike)
actions.aoe+=/frost_nova,if=freezable&!prev.1.freeze&(prev_gcd.1.glacial_spike&!remaining_winters_chill)
actions.aoe+=/shifting_power,if=cooldown.comet_storm.remains>10
actions.aoe+=/frostbolt,if=buff.frostfire_empowerment.up&!buff.excess_frost.up&!buff.excess_fire.up
actions.aoe+=/flurry,if=cooldown_react&!remaining_winters_chill&(buff.brain_freeze.up&!talent.excess_frost|buff.excess_frost.up)
actions.aoe+=/ice_lance,if=buff.fingers_of_frost.up|debuff.frozen.remains>travel_time|remaining_winters_chill
actions.aoe+=/flurry,if=cooldown_react&!remaining_winters_chill
actions.aoe+=/ice_nova,if=active_enemies>=4&(!talent.glacial_spike|!freezable)&!talent.frostfire_bolt
actions.aoe+=/cone_of_cold,if=target.within10&!talent.coldest_snap&active_enemies>=7
actions.aoe+=/frostbolt
actions.aoe+=/call_action_list,name=movement

actions.aoe+=/cone_of_cold,if=talent.coldest_snap&cooldown.frozen_orb.remains>4&(prev_gcd.1.comet_storm|prev_gcd.1.frozen_orb&!talent.comet_storm|cooldown.comet_storm.remains>15&!talent.frostfire_bolt)
actions.aoe+=/frozen_orb,if=cooldown_react&((!prev_gcd.1.cone_of_cold|!talent.isothermic_core)&(!prev_gcd.1.glacial_spike|!freezable))
actions.aoe+=/blizzard,if=!prev_gcd.1.glacial_spike|!freezable
actions.aoe+=/frostbolt,if=buff.icy_veins.up&(buff.deaths_chill.stack<9|buff.deaths_chill.stack=9&!action.frostbolt.in_flight)&buff.icy_veins.remains>8&talent.deaths_chill
actions.aoe+=/comet_storm,if=!prev_gcd.1.glacial_spike&(!talent.coldest_snap|cooldown.cone_of_cold.ready&cooldown.frozen_orb.remains>20|(cooldown.cone_of_cold.remains>variable.time_to_coc))
actions.aoe+=/freeze,if=freezable&debuff.frozen.down&(!talent.glacial_spike|prev_gcd.1.glacial_spike)
actions.aoe+=/ice_nova,if=freezable&!prev.1.freeze&(prev_gcd.1.glacial_spike)
actions.aoe+=/frost_nova,if=freezable&!prev.1.freeze&(prev_gcd.1.glacial_spike&!remaining_winters_chill)
actions.aoe+=/shifting_power,if=cooldown.comet_storm.remains>14
actions.aoe+=/frostbolt,if=buff.frostfire_empowerment.up&!buff.excess_frost.up&!buff.excess_fire.up
actions.aoe+=/glacial_spike,if=buff.icicles.stacks=5&(freezable|(action.flurry.cooldown_react|remaining_winters_chill))
actions.aoe+=/flurry,if=cooldown_react&!remaining_winters_chill&(buff.brain_freeze.up&!talent.excess_frost|buff.excess_frost.up|prev_gcd.1.glacial_spike)
actions.aoe+=/ice_lance,if=buff.fingers_of_frost.up|debuff.frozen.remains>travel_time|remaining_winters_chill
actions.aoe+=/flurry,if=cooldown_react&!remaining_winters_chill
actions.aoe+=/ice_nova,if=active_enemies>=4&(!talent.glacial_spike|!freezable)&!talent.frostfire_bolt
actions.aoe+=/cone_of_cold,if=!talent.coldest_snap&active_enemies>=7
actions.aoe+=/frostbolt
actions.aoe+=/call_action_list,name=movement

actions.cds+=/use_item,name=imperfect_ascendancy_serum,if=buff.icy_veins.remains>19|boss&fight_remains<25
actions.cds+=/use_item,name=spymasters_web,if=boss&fight_remains<25|(boss&fight_remains<100|buff.spymasters_report.stack>35)&(!talent.deaths_chill&buff.icy_veins.remains>19|talent.deaths_chill&buff.icy_veins.remains>15&buff.icy_veins.remains<20)
actions.cds+=/potion,if=prev.1.icy_veins|boss&fight_remains<60
actions.cds+=/flurry,if=time<0.1&active_enemies<=2
actions.cds+=/icy_veins
actions.cds+=/use_items
## actions.cds+=/invoke_external_buff,name=power_infusion,if=buff.power_infusion.down
## actions.cds+=/invoke_external_buff,name=blessing_of_summer,if=buff.blessing_of_summer.down
actions.cds+=/blood_fury
actions.cds+=/berserking
actions.cds+=/lights_judgment
actions.cds+=/fireblood
actions.cds+=/ancestral_call

actions.cleave_ff+=/comet_storm,if=prev_gcd.1.flurry|prev_gcd.1.cone_of_cold
actions.cleave_ff+=/frostbolt,if=buff.icy_veins.up&(buff.deaths_chill.stack<8|buff.deaths_chill.stack=8&!action.frostbolt.in_flight)&buff.icy_veins.remains>8&talent.deaths_chill
actions.cleave_ff+=/flurry,if=cooldown_react&(((prev_gcd.1.frostbolt|prev_gcd.1.frostfire_bolt)&buff.icicles.stacks>=3)|prev_gcd.1.glacial_spike|(buff.icicles.stacks>=3&buff.icicles.stacks<5&charges_fractional=2))
actions.cleave_ff+=/ray_of_frost,if=remaining_winters_chill=1
actions.cleave_ff+=/frozen_orb,if=buff.fingers_of_frost.stacks<2&(!talent.ray_of_frost|cooldown.ray_of_frost.remains)
actions.cleave_ff+=/cone_of_cold,if=talent.coldest_snap&cooldown.comet_storm.remains>10&cooldown.frozen_orb.remains>10&remaining_winters_chill=0&active_enemies>=3
actions.cleave_ff+=/glacial_spike,if=buff.icicles.stacks=5&(action.flurry.cooldown_react|remaining_winters_chill&buff.icy_veins.down)
actions.cleave_ff+=/shifting_power,if=cooldown.frozen_orb.remains>10&(!talent.comet_storm|cooldown.comet_storm.remains>10)&(!talent.ray_of_frost|cooldown.ray_of_frost.remains>10)|cooldown.icy_veins.remains<20
actions.cleave_ff+=/glacial_spike,if=buff.icicles.stacks=5
actions.cleave_ff+=/frostfire_bolt,if=buff.frostfire_empowerment.up&remaining_winters_chill
actions.cleave_ff+=/ice_lance,if=buff.fingers_of_frost.react&!prev_gcd.1.glacial_spike|remaining_winters_chill&buff.icy_veins.down
actions.cleave_ff+=/frostbolt
actions.cleave_ff+=/call_action_list,name=movement

actions.cleave_ss+=/comet_storm,if=prev_gcd.1.flurry&(buff.icy_veins.down)|prev_gcd.1.cone_of_cold|active_enemies>=3
actions.cleave_ss+=/flurry,if=cooldown_react&remaining_winters_chill=0&debuff.winters_chill.down&(prev_gcd.1.frostbolt|prev_gcd.1.glacial_spike)
actions.cleave_ss+=/flurry,if=cooldown_react&prev_gcd.1.glacial_spike
actions.cleave_ss+=/freeze,if=freezable&debuff.frozen.down&(!talent.glacial_spike|prev_gcd.1.glacial_spike)
actions.cleave_ss+=/ice_nova,if=freezable&!prev.1.freeze&(prev_gcd.1.glacial_spike)
actions.cleave_ss+=/frozen_orb,if=cooldown_react
actions.cleave_ss+=/shifting_power,if=cooldown.icy_veins.remains>10&cooldown.flurry.remains&(boss&fight_remains>cooldown.icy_veins.remains-6)
actions.cleave_ss+=/cone_of_cold,if=talent.coldest_snap&cooldown.comet_storm.remains>10&cooldown.frozen_orb.remains>10&remaining_winters_chill=0&active_enemies>=3
actions.cleave_ss+=/glacial_spike,if=buff.icicles.stacks=5&((action.flurry.cooldown_react|remaining_winters_chill)|freezable&(cooldown.freeze.ready|cooldown.ice_nova.ready))
actions.cleave_ss+=/ray_of_frost,if=remaining_winters_chill&buff.icy_veins.down
actions.cleave_ss+=/frostbolt,if=talent.deaths_chill&(!talent.freezing_rain&buff.icy_veins.remains>8&buff.deaths_chill.stack<=13|talent.freezing_rain&buff.icy_veins.remains>22)
actions.cleave_ss+=/ice_lance,if=buff.fingers_of_frost.up&!prev_gcd.1.glacial_spike
actions.cleave_ss+=/blizzard,if=talent.ice_caller&talent.freezing_rain&active_enemies>=3
actions.cleave_ss+=/frostbolt
actions.cleave_ss+=/call_action_list,name=movement

actions.movement+=/any_blink,if=movement.distance>10
actions.movement+=/ice_floes,if=buff.ice_floes.down
actions.movement+=/ice_nova
actions.movement+=/cone_of_cold,if=!talent.coldest_snap&active_enemies>=2
actions.movement+=/arcane_explosion,if=mana.pct>30&active_enemies>=2
actions.movement+=/fire_blast
actions.movement+=/ice_lance

actions.st_aoebuild+=/comet_storm,if=prev_gcd.1.flurry&(buff.icy_veins.down|talent.frostfire_bolt)
actions.st_aoebuild+=/flurry,if=cooldown_react&(buff.icicles.stacks<5|talent.splinterstorm)&(remaining_winters_chill=0&debuff.winters_chill.down&(prev_gcd.1.frostbolt|prev_gcd.1.frostfire_bolt|prev_gcd.1.glacial_spike)|buff.excess_frost.react)
actions.st_aoebuild+=/frozen_orb,if=cooldown_react&(talent.splinterstorm|(!talent.ray_of_frost|buff.fingers_of_frost.down&cooldown.ray_of_frost.remains&buff.icicles.stacks<5))
actions.st_aoebuild+=/shifting_power,if=(cooldown.icy_veins.remains>10&cooldown.flurry.remains&(boss&fight_remains>cooldown.icy_veins.remains-6)|talent.frostfire_bolt)&(talent.splinterstorm|(buff.icy_veins.down|!talent.deaths_chill)&cooldown.frozen_orb.remains>10&(!talent.comet_storm|cooldown.comet_storm.remains>10)&(!talent.ray_of_frost|cooldown.ray_of_frost.remains>10)&buff.icicles.stacks<5)
actions.st_aoebuild+=/glacial_spike,if=buff.icicles.stacks=5&(action.flurry.cooldown_react|remaining_winters_chill)
actions.st_aoebuild+=/ray_of_frost,if=remaining_winters_chill
actions.st_aoebuild+=/ice_lance,if=buff.fingers_of_frost.react&!prev_gcd.1.glacial_spike|remaining_winters_chill
actions.st_aoebuild+=/frostbolt
actions.st_aoebuild+=/call_action_list,name=movement

actions.st_ff+=/comet_storm,if=prev_gcd.1.flurry|prev_gcd.1.cone_of_cold
actions.st_ff+=/flurry,if=cooldown_react&buff.icicles.stacks<5&remaining_winters_chill=0&(debuff.winters_chill.down|buff.brain_freeze.react|buff.excess_frost.react)
actions.st_ff+=/cone_of_cold,if=talent.coldest_snap&prev_gcd.1.comet_storm&active_enemies>=3
actions.st_ff+=/glacial_spike,if=buff.icicles.stacks=5&(action.flurry.cooldown_react|remaining_winters_chill)
actions.st_ff+=/ray_of_frost,if=remaining_winters_chill&(buff.icy_veins.remains<14|buff.spymasters_web.up)
actions.st_ff+=/frozen_orb
actions.st_ff+=/shifting_power,if=(buff.icy_veins.down|!talent.deaths_chill)&cooldown.frozen_orb.remains>10&(!talent.comet_storm|cooldown.comet_storm.remains>10)&(!talent.ray_of_frost|cooldown.ray_of_frost.remains>10)
actions.st_ff+=/ice_lance,if=buff.excess_fire.react&remaining_winters_chill=2|remaining_winters_chill=0&debuff.winters_chill.down&buff.fingers_of_frost.react
actions.st_ff+=/frostbolt
actions.st_ff+=/call_action_list,name=movement

actions.st_ss+=/comet_storm,if=prev_gcd.1.flurry&buff.icy_veins.down
actions.st_ss+=/flurry,if=cooldown_react&remaining_winters_chill=0&debuff.winters_chill.down&(prev_gcd.1.frostbolt|prev_gcd.1.glacial_spike)
actions.st_ss+=/frozen_orb,if=cooldown_react
actions.st_ss+=/shifting_power,if=cooldown.icy_veins.remains>10&cooldown.flurry.remains&(boss&fight_remains>cooldown.icy_veins.remains-6)
actions.st_ss+=/glacial_spike,if=buff.icicles.stacks=5&(action.flurry.cooldown_react|remaining_winters_chill)
actions.st_ss+=/ray_of_frost,if=remaining_winters_chill&buff.icy_veins.down
actions.st_ss+=/frostbolt,if=buff.icy_veins.remains>8&buff.deaths_chill.stack<8
actions.st_ss+=/ice_lance,if=remaining_winters_chill=2|remaining_winters_chill&action.flurry.cooldown_react
actions.st_ss+=/frostbolt
actions.st_ss+=/call_action_list,name=movement