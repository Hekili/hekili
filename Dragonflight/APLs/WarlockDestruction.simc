actions.precombat=fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/variable,name=opti_cc,op=set,value=talent.crashing_chaos&equipped.neltharions_call_to_dominance
actions.precombat+=/use_item,name=tome_of_monstrous_constructions
actions.precombat+=/use_item,name=soleahs_secret_technique
actions.precombat+=/grimoire_of_sacrifice,if=talent.grimoire_of_sacrifice.enabled
actions.precombat+=/use_item,name=shadowed_orb_of_torment
actions.precombat+=/soul_fire
actions.precombat+=/cataclysm,if=raid_event.adds.in>15
actions.precombat+=/incinerate

actions=spell_lock
# Simc thinks that infernal is active the moment it lands, but we prefer to have it active when the spell is used
actions+=/variable,name=infernal_active,op=set,value=pet.infernal.active|cooldown.summon_infernal.remains>160
actions+=/call_action_list,name=aoe,if=(active_enemies>=3-(talent.inferno&!talent.madness_of_the_azjaqir))&!(!talent.inferno&talent.madness_of_the_azjaqir&talent.chaos_incarnate&active_enemies<4)&!settings.cleave_apl
actions+=/call_action_list,name=cleave,if=active_enemies!=1|settings.cleave_apl
actions+=/call_action_list,name=ogcd
actions+=/call_action_list,name=items
## actions+=/variable,name=havoc_immo_time,op=set,condition=active_havoc,value=dot.immolate.remains,value_else=0
## actions+=/cycling_variable,name=havoc_immo_time,op=add,value=dot.immolate.remains*debuff.havoc.up
actions+=/call_action_list,name=aoe,strict=1,if=!settings.cleave_apl&(active_enemies>=3-(talent.inferno&!talent.madness_of_the_azjaqir))&!(!talent.inferno&talent.madness_of_the_azjaqir&talent.chaos_incarnate&active_enemies<4)
actions+=/call_action_list,name=cleave,strict=1,if=settings.cleave_apl&active_enemies>1
actions+=/conflagrate,if=(talent.roaring_blaze&debuff.conflagrate.remains<1.5)|charges=max_charges
actions+=/dimensional_rift,if=soul_shard<4.7&(charges>2|boss&fight_remains<cooldown)
actions+=/cataclysm,if=raid_event.adds.in>15
actions+=/channel_demonfire,if=talent.raging_demonfire&(dot.immolate.remains-5*(action.chaos_bolt.in_flight&talent.internal_combustion))>cast_time&(debuff.conflagrate.remains>execute_time|!talent.roaring_blaze)
actions+=/soul_fire,if=soul_shard<=3.5&(debuff.conflagrate.remains>cast_time+travel_time|!talent.roaring_blaze&buff.backdraft.up)
actions+=/immolate,if=(((dot.immolate.remains-5*(action.chaos_bolt.in_flight&talent.internal_combustion))<dot.immolate.duration*0.3)|dot.immolate.remains<3|(dot.immolate.remains-action.chaos_bolt.execute_time)<5&talent.infernal_combustion&action.chaos_bolt.usable)&(!talent.cataclysm|cooldown.cataclysm.remains>dot.immolate.remains)&(!talent.soul_fire|cooldown.soul_fire.remains+action.soul_fire.cast_time>(dot.immolate.remains-5*talent.internal_combustion))&target.time_to_die>8
## Removed from SimC priority as of 2023-05-30.
## actions+=/havoc,if=talent.cry_havoc&((buff.ritual_of_ruin.up&pet.infernal.active&talent.burn_to_ashes)|((buff.ritual_of_ruin.up|pet.infernal.active)&!talent.burn_to_ashes))
actions+=/channel_demonfire,if=dot.immolate.remains>cast_time&set_bonus.tier30_4pc
# We want to pool some soul_shard if we play with cc before casting infernal and then chain the madness into first cc proc
actions+=/chaos_bolt,if=cooldown.summon_infernal.remains=0&soul_shard>4&(trinket.spoils_of_neltharus.ready_cooldown|!equipped.spoils_of_neltharus)&buff.domineering_arrogance.stack<3&talent.crashing_chaos
# With crushing_chaos we want to be sure that infrnal lines with the next comming trinket, if it's ready soon, and that we have pulled shards or have madness up
actions+=/summon_infernal,if=variable.opti_cc&((soul_shard>4&buff.domineering_arrogance.stack>=3|buff.domineering_arrogance.stack<3&buff.madness_cb.remains>2*gcd.max)&(trinket.spoils_of_neltharus.cooldown.remains<2|!equipped.spoils_of_neltharus))|!variable.opti_cc|fight_remains<30
actions+=/chaos_bolt,if=pet.infernal.active|pet.blasphemy.active|soul_shard>=4&(variable.opti_cc&(cooldown.summon_infernal.remains<?trinket.spoils_of_neltharus.cooldown.remains)>2|!variable.opti_cc)
actions+=/channel_demonfire,if=talent.ruin.rank>1&!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))&dot.immolate.remains>cast_time
actions+=/conflagrate,if=buff.backdraft.down&soul_shard>=1.5&!talent.roaring_blaze
actions+=/incinerate,if=cast_time+action.chaos_bolt.cast_time<buff.madness_cb.remains&(buff.call_to_dominance.down|!variable.opti_cc)
actions+=/chaos_bolt,if=buff.rain_of_chaos.remains>cast_time
actions+=/chaos_bolt,if=buff.backdraft.up&!talent.eradication&!talent.madness_of_the_azjaqir
actions+=/chaos_bolt,if=buff.madness_cb.up&((cooldown.summon_infernal.remains<?trinket.spoils_of_neltharus.cooldown.remains)>10|!variable.opti_cc)
actions+=/channel_demonfire,if=!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))&dot.immolate.remains>cast_time
actions+=/dimensional_rift
actions+=/chaos_bolt,if=soul_shard>3.5&(cooldown.summon_infernal.remains_expected>5|!variable.opti_cc)
actions+=/chaos_bolt,if=talent.soul_conduit&!talent.madness_of_the_azjaqir|!talent.backdraft
actions+=/chaos_bolt,if=boss&fight_remains<5&fight_remains>cast_time+travel_time
actions+=/conflagrate,if=charges>(max_charges-1)|fight_remains<gcd.max*charges
actions+=/incinerate

actions.aoe+=/call_action_list,name=havoc,strict=1,if=havoc_active&havoc_remains>gcd.max&active_enemies<5+(talent.cry_havoc&!talent.inferno)&(!cooldown.summon_infernal.up|!talent.summon_infernal)
actions.aoe+=/rain_of_fire,if=pet.infernal.active|pet.blasphemy.active
actions.aoe+=/rain_of_fire,if=fight_remains<12
actions.aoe+=/rain_of_fire,if=gcd.max>buff.madness_rof.remains&buff.madness_rof.up
actions.aoe+=/rain_of_fire,if=soul_shard>=(4.5-0.1*active_dot.immolate)&time>5
actions.aoe+=/chaos_bolt,if=soul_shard>3.5-(0.1*active_enemies)&!talent.rain_of_fire
actions.aoe+=/cataclysm,if=raid_event.adds.in>15
actions.aoe+=/havoc,cycle_targets=1,if=cycle_enemies>1&(!cooldown.summon_infernal.up|!talent.summon_infernal|(talent.inferno&active_enemies>4))
actions.aoe+=/immolate,cycle_targets=1,if=dot.immolate.refreshable&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>dot.immolate.remains|time<5)&(!talent.raging_demonfire|cooldown.channel_demonfire.remains>remains)&active_dot.immolate<=cycle_enemies&!havoc_active&target.time_to_die>18
actions.aoe+=/channel_demonfire,if=dot.immolate.remains>cast_time&talent.raging_demonfire
actions.aoe+=/summon_soulkeeper,if=buff.tormented_soul.stack=10|buff.tormented_soul.stack>3&boss&fight_remains<10
actions.aoe+=/summon_infernal,if=cooldown.invoke_power_infusion_0.up|cooldown.invoke_power_infusion_0.duration=0|fight_remains>=190&!talent.grand_warlocks_design
actions.aoe+=/rain_of_fire,if=debuff.pyrogenics.down&active_enemies<=4
actions.aoe+=/channel_demonfire,if=dot.immolate.remains>cast_time
actions.aoe+=/immolate,cycle_targets=1,if=((dot.immolate.refreshable&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>dot.immolate.remains))|cycle_enemies>active_dot.immolate)&target.time_to_die>10&!havoc_active
actions.aoe+=/immolate,cycle_targets=1,if=(dot.immolate.refreshable|(dot.immolate.remains<2&dot.immolate.remains<havoc_remains)|!dot.immolate.ticking)&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>dot.immolate.remains)&target.time_to_die>11
actions.aoe+=/soul_fire,if=buff.backdraft.up
actions.aoe+=/incinerate,if=talent.fire_and_brimstone.enabled&buff.backdraft.up
actions.aoe+=/conflagrate,if=buff.backdraft.stack<2|!talent.backdraft
actions.aoe+=/dimensional_rift
actions.aoe+=/incinerate

actions.cleave=call_action_list,name=havoc,strict=1,if=havoc_active&havoc_remains>gcd.max
actions.cleave+=/variable,name=pool_soul_shards,value=cooldown.havoc.remains<=10|talent.mayhem
actions.cleave+=/conflagrate,if=(talent.roaring_blaze.enabled&debuff.conflagrate.remains<1.5)|charges=max_charges
actions.cleave+=/dimensional_rift,if=soul_shard<4.7&(charges>2|boss&fight_remains<cooldown)
actions.cleave+=/cataclysm,if=raid_event.adds.in>15
actions.cleave+=/channel_demonfire,if=talent.raging_demonfire&active_dot.immolate>1
actions.cleave+=/soul_fire,if=soul_shard<=3.5&(debuff.conflagrate.remains>cast_time+travel_time|!talent.roaring_blaze&buff.backdraft.up)&!variable.pool_soul_shards
# Warning: It's really important to put Immolate on 2 targets for Channel Demonfire, so disabling target swaps may be really problematic.
actions.cleave+=/immolate,cycle_targets=1,if=refreshable&(dot.immolate.remains<cooldown.havoc.remains|!dot.immolate.ticking)&(!talent.cataclysm|cooldown.cataclysm.remains>remains)&(!talent.soul_fire|cooldown.soul_fire.remains+(!talent.mayhem*action.soul_fire.cast_time)>dot.immolate.remains)&target.time_to_die>15
actions.cleave+=/havoc,cycle_targets=1,if=(!cooldown.summon_infernal.up|!talent.summon_infernal)&target.time_to_die>8
actions.cleave+=/chaos_bolt,if=pet.infernal.active|pet.blasphemy.active|soul_shard>=4
actions.cleave+=/summon_infernal
actions.cleave+=/channel_demonfire,if=talent.ruin.rank>1&!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))
actions.cleave+=/conflagrate,if=buff.backdraft.down&soul_shard>=1.5&!variable.pool_soul_shards
actions.cleave+=/incinerate,if=cast_time+action.chaos_bolt.cast_time<buff.madness_cb.remains
actions.cleave+=/chaos_bolt,if=buff.rain_of_chaos.remains>cast_time
actions.cleave+=/chaos_bolt,if=buff.backdraft.up&!variable.pool_soul_shards
actions.cleave+=/chaos_bolt,if=talent.eradication&!variable.pool_soul_shards&debuff.eradication.remains<cast_time&!action.chaos_bolt.in_flight
actions.cleave+=/chaos_bolt,if=buff.madness_cb.up&(!variable.pool_soul_shards|talent.burn_to_ashes&buff.burn_to_ashes.stack=0|talent.soul_fire)
actions.cleave+=/soul_fire,if=soul_shard<=4&talent.mayhem
actions.cleave+=/channel_demonfire,if=!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))
actions.cleave+=/dimensional_rift
actions.cleave+=/chaos_bolt,if=soul_shard>3.5&!variable.pool_soul_shards
actions.cleave+=/chaos_bolt,if=!variable.pool_soul_shards&(talent.soul_conduit&!talent.madness_of_the_azjaqir|!talent.backdraft)
actions.cleave+=/chaos_bolt,if=boss&fight_remains<5&fight_remains>cast_time+travel_time
actions.cleave+=/summon_soulkeeper,if=buff.tormented_soul.stack=10|buff.tormented_soul.stack>3&boss&fight_remains<10
actions.cleave+=/conflagrate,if=charges>(max_charges-1)|fight_remains<gcd.max*charges
actions.cleave+=/incinerate

actions.havoc=conflagrate,if=talent.backdraft&buff.backdraft.down&soul_shard>=1&soul_shard<=4
actions.havoc+=/soul_fire,if=cast_time<havoc_remains&soul_shard<2.5
actions.havoc+=/channel_demonfire,if=soul_shard<4.5&talent.raging_demonfire.rank=2
actions.havoc+=/immolate,cycle_targets=1,if=((dot.immolate.refreshable&target.time_to_die>5)|((dot.immolate.remains<2&dot.immolate.remains<havoc_remains)|!dot.immolate.ticking)&target.time_to_die>11)&soul_shard<4.5
actions.havoc+=/chaos_bolt,if=((talent.cry_havoc&!talent.inferno)|!talent.rain_of_fire)&cast_time<havoc_remains
actions.havoc+=/chaos_bolt,if=cast_time<havoc_remains&(active_enemies<=3-talent.inferno+(talent.madness_of_the_azjaqir&!talent.inferno))
actions.havoc+=/rain_of_fire,if=active_enemies>=3&talent.inferno
actions.havoc+=/rain_of_fire,if=(active_enemies>=4-talent.inferno+talent.madness_of_the_azjaqir)
actions.havoc+=/rain_of_fire,if=active_enemies>2&(talent.avatar_of_destruction|talent.rain_of_chaos&buff.rain_of_chaos.up)&talent.inferno.enabled
actions.havoc+=/channel_demonfire,if=soul_shard<4.5
actions.havoc+=/conflagrate,if=!talent.backdraft
actions.havoc+=/incinerate,if=cast_time<havoc_remains

actions.items+=/use_item,slot=trinket1,if=variable.infernal_active|!talent.summon_infernal|fight_remains<21|trinket.1.cooldown.duration<cooldown.summon_infernal.remains_expected+5
actions.items+=/use_item,slot=trinket2,if=variable.infernal_active|!talent.summon_infernal|fight_remains<21|trinket.2.cooldown.duration<cooldown.summon_infernal.remains_expected+5
actions.items+=/use_item,name=spoils_of_neltharus,if=(!talent.crashing_chaos&(fight_remains>trinket.spoils_of_neltharus.cooldown.duration)|cooldown.summon_infernal.remains_expected>fight_remains-25)|variable.infernal_active
actions.items+=/use_item,slot=trinket1,if=(!talent.rain_of_chaos&fight_remains<cooldown.summon_infernal.remains_expected+trinket.1.cooldown.duration&fight_remains>trinket.1.cooldown.duration)|fight_remains<cooldown.summon_infernal.remains_expected|(trinket.2.has_cooldown&trinket.2.cooldown.remains<cooldown.summon_infernal.remains_expected)
actions.items+=/use_item,slot=trinket2,if=(!talent.rain_of_chaos&fight_remains<cooldown.summon_infernal.remains_expected+trinket.2.cooldown.duration&fight_remains>trinket.2.cooldown.duration)|fight_remains<cooldown.summon_infernal.remains_expected|(trinket.1.has_cooldown&trinket.1.cooldown.remains<cooldown.summon_infernal.remains_expected)
actions.items+=/use_item,name=erupting_spear_fragment,if=(!talent.rain_of_chaos&fight_remains<cooldown.summon_infernal.remains_expected+trinket.erupting_spear_fragment.cooldown.duration&fight_remains>trinket.erupting_spear_fragment.cooldown.duration)|fight_remains<cooldown.summon_infernal.remains_expected|trinket.erupting_spear_fragment.cooldown.duration<cooldown.summon_infernal.remains_expected+5
actions.items+=/use_item,name=desperate_invokers_codex
actions.items+=/use_item,name=iceblood_deathsnare
actions.items+=/use_item,name=conjured_chillglobe
actions.items+=/use_item,name=iridal_the_earths_master

actions.ogcd+=/potion,use_off_gcd=1,if=variable.infernal_active|!talent.summon_infernal
## actions.ogcd+=/invoke_external_buff,name=power_infusion,if=variable.infernal_active|!talent.summon_infernal|(fight_remains<cooldown.summon_infernal.remains_expected+10+cooldown.invoke_power_infusion_0.duration&fight_remains>cooldown.invoke_power_infusion_0.duration)|fight_remains<cooldown.summon_infernal.remains_expected+15
actions.ogcd+=/berserking,use_off_gcd=1,if=variable.infernal_active|!talent.summon_infernal|(fight_remains<(cooldown.summon_infernal.remains_expected+cooldown.berserking.duration)&(fight_remains>cooldown.berserking.duration))|fight_remains<cooldown.summon_infernal.remains_expected
actions.ogcd+=/blood_fury,use_off_gcd=1,if=variable.infernal_active|!talent.summon_infernal|(fight_remains<cooldown.summon_infernal.remains_expected+10+cooldown.blood_fury.duration&fight_remains>cooldown.blood_fury.duration)|fight_remains<cooldown.summon_infernal.remains
actions.ogcd+=/fireblood,use_off_gcd=1,if=variable.infernal_active|!talent.summon_infernal|(fight_remains<cooldown.summon_infernal.remains_expected+10+cooldown.fireblood.duration&fight_remains>cooldown.fireblood.duration)|fight_remains<cooldown.summon_infernal.remains_expected
actions.ogcd+=/ancestral_call,use_off_gcd=1,if=variable.infernal_active|!talent.summon_infernal|(fight_remains<(cooldown.summon_infernal.remains_expected+cooldown.berserking.duration)&(fight_remains>cooldown.berserking.duration))|fight_remains<cooldown.summon_infernal.remains_expected