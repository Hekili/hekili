actions.precombat=fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/use_item,name=tome_of_monstrous_constructions
actions.precombat+=/use_item,name=soleahs_secret_technique
actions.precombat+=/grimoire_of_sacrifice,if=talent.grimoire_of_sacrifice.enabled
actions.precombat+=/use_item,name=shadowed_orb_of_torment
actions.precombat+=/soul_fire
actions.precombat+=/cataclysm
actions.precombat+=/incinerate

actions=spell_lock
actions+=/call_action_list,name=items
actions+=/call_action_list,name=ogcd
actions+=/call_action_list,name=aoe,if=(active_enemies>=3-(talent.inferno&!talent.madness_of_the_azjaqir))&!(!talent.inferno&talent.madness_of_the_azjaqir&talent.chaos_incarnate&active_enemies<4)
## cleave_apl is hard set to 0 in sim priority, so fully disabling here to avoid wasted CPU time
## actions+=/call_action_list,name=cleave,if=active_enemies!=1|variable.cleave_apl
actions+=/conflagrate,if=(talent.roaring_blaze&debuff.conflagrate.remains<1.5)|charges=max_charges
actions+=/dimensional_rift,if=soul_shard<4.7&(charges>2|boss&fight_remains<cooldown.dimensional_rift.duration)
actions+=/cataclysm
actions+=/channel_demonfire,if=talent.raging_demonfire
actions+=/soul_fire,if=soul_shard<=3.5&(debuff.conflagrate.remains>cast_time+travel_time|!talent.roaring_blaze&buff.backdraft.up)
actions+=/immolate,if=((dot.immolate.refreshable&talent.internal_combustion)|dot.immolate.remains<3)&(!talent.cataclysm|cooldown.cataclysm.remains>dot.immolate.remains)&(!talent.soul_fire|cooldown.soul_fire.remains+action.soul_fire.cast_time>dot.immolate.remains)&target.time_to_die>8
actions+=/havoc,if=talent.cry_havoc&((buff.ritual_of_ruin.up&pet.infernal.active&talent.burn_to_ashes)|((buff.ritual_of_ruin.up|pet.infernal.active)&!talent.burn_to_ashes))
actions+=/chaos_bolt,if=pet.infernal.active|pet.blasphemy.active|soul_shard>=4
actions+=/summon_infernal
actions+=/channel_demonfire,if=talent.ruin.rank>1&!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))&dot.immolate.remains>cast_time
actions+=/conflagrate,if=buff.backdraft.down&soul_shard>=1.5&!talent.roaring_blaze
actions+=/incinerate,if=buff.burn_to_ashes.up&cast_time+action.chaos_bolt.cast_time<buff.madness_cb.remains
actions+=/chaos_bolt,if=buff.rain_of_chaos.remains>cast_time
actions+=/chaos_bolt,if=buff.backdraft.up&!talent.eradication&!talent.madness_of_the_azjaqir
actions+=/chaos_bolt,if=buff.madness_cb.up
actions+=/channel_demonfire,if=!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))&dot.immolate.remains>cast_time
actions+=/dimensional_rift
actions+=/chaos_bolt,if=soul_shard>3.5
actions+=/chaos_bolt,if=talent.soul_conduit&!talent.madness_of_the_azjaqir|!talent.backdraft
actions+=/chaos_bolt,if=boss&fight_remains<5&time_to_die>cast_time+travel_time
actions+=/conflagrate,if=charges>(max_charges-1)|boss&fight_remains<gcd*charges
actions+=/incinerate

actions.aoe+=/call_action_list,name=havoc,strict=1,if=havoc_active&havoc_remains>gcd&active_enemies<5+(talent.cry_havoc&!talent.inferno)
actions.aoe+=/rain_of_fire,if=pet.infernal.active
actions.aoe+=/rain_of_fire,if=pet.blasphemy.active
actions.aoe+=/rain_of_fire,if=soul_shard>=(4.5-0.1*active_dot.immolate)
actions.aoe+=/chaos_bolt,if=soul_shard>3.5-(0.1*active_enemies)&!talent.rain_of_fire
actions.aoe+=/cataclysm
actions.aoe+=/channel_demonfire,if=dot.immolate.remains>cast_time&talent.raging_demonfire
actions.aoe+=/immolate,cycle_targets=1,if=dot.immolate.remains<5&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>dot.immolate.remains)&(!talent.raging_demonfire|cooldown.channel_demonfire.remains>remains)&active_dot.immolate<=4&!havoc_active&target.time_to_die>8
actions.aoe+=/summon_soulkeeper,if=buff.tormented_soul.stack=10|buff.tormented_soul.stack>3&boss&fight_remains<10
actions.aoe+=/summon_infernal
actions.aoe+=/rain_of_fire,if=(debuff.pyrogenics.down|buff.madness_rof.up)&active_enemies<=4
actions.aoe+=/havoc,cycle_targets=1,if=cycle_enemies>1
actions.aoe+=/channel_demonfire,if=dot.immolate.remains>cast_time
actions.aoe+=/immolate,cycle_targets=1,if=((dot.immolate.remains<3&(!talent.cataclysm.enabled|cooldown.cataclysm.remains>dot.immolate.remains))|cycle_enemies>active_dot.immolate)&target.time_to_die>8
actions.aoe+=/soul_fire,if=buff.backdraft.up
actions.aoe+=/incinerate,if=talent.fire_and_brimstone.enabled&buff.backdraft.up
actions.aoe+=/conflagrate,if=buff.backdraft.stack<2|!talent.backdraft
actions.aoe+=/dimensional_rift
actions.aoe+=/incinerate

actions.cleave=call_action_list,name=havoc,strict=1,if=havoc_active&havoc_remains>gcd
actions.cleave+=/variable,name=pool_soul_shards,value=cooldown.havoc.remains<=10|talent.mayhem
actions.cleave+=/conflagrate,if=(talent.roaring_blaze.enabled&debuff.conflagrate.remains<1.5)|charges=max_charges
actions.cleave+=/dimensional_rift,if=soul_shard<4.7&(charges>2|boss&fight_remains<cooldown.dimensional_rift.duration)
actions.cleave+=/cataclysm
actions.cleave+=/channel_demonfire,if=talent.raging_demonfire
actions.cleave+=/soul_fire,if=soul_shard<=3.5&(debuff.conflagrate.remains>cast_time+travel_time|!talent.roaring_blaze&buff.backdraft.up)&!variable.pool_soul_shards
actions.cleave+=/immolate,cycle_targets=1,if=refreshable&((talent.internal_combustion&dot.immolate.refreshable)|dot.immolate.remains<3)&(!talent.cataclysm|cooldown.cataclysm.remains>remains)&(!talent.soul_fire|cooldown.soul_fire.remains+(!talent.mayhem*action.soul_fire.cast_time)>dot.immolate.remains)&time_to_die>8
actions.cleave+=/havoc,cycle_targets=1,if=(!cooldown.summon_infernal.up|!talent.summon_infernal)
actions.cleave+=/chaos_bolt,if=pet.infernal.active|pet.blasphemy.active|soul_shard>=4
actions.cleave+=/summon_infernal
actions.cleave+=/channel_demonfire,if=talent.ruin.rank>1&!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))
actions.cleave+=/conflagrate,if=buff.backdraft.down&soul_shard>=1.5&!variable.pool_soul_shards
actions.cleave+=/incinerate,if=buff.burn_to_ashes.up&cast_time+action.chaos_bolt.cast_time<buff.madness_cb.remains
actions.cleave+=/chaos_bolt,if=buff.rain_of_chaos.remains>cast_time
actions.cleave+=/chaos_bolt,if=buff.backdraft.up&!variable.pool_soul_shards
actions.cleave+=/chaos_bolt,if=talent.eradication&!variable.pool_soul_shards&debuff.eradication.remains<cast_time&!action.chaos_bolt.in_flight
actions.cleave+=/chaos_bolt,if=buff.madness_cb.up
actions.cleave+=/soul_fire,if=soul_shard<=4&talent.mayhem
actions.cleave+=/channel_demonfire,if=!(talent.diabolic_embers&talent.avatar_of_destruction&(talent.burn_to_ashes|talent.chaos_incarnate))
actions.cleave+=/dimensional_rift
actions.cleave+=/chaos_bolt,if=soul_shard>3.5
actions.cleave+=/chaos_bolt,if=!variable.pool_soul_shards&(talent.soul_conduit&!talent.madness_of_the_azjaqir|!talent.backdraft)
actions.cleave+=/chaos_bolt,if=boss&fight_remains<5&time_to_die>cast_time+travel_time
actions.cleave+=/summon_soulkeeper,if=buff.tormented_soul.stack=10|buff.tormented_soul.stack>3&time_to_die<10
actions.cleave+=/conflagrate,if=charges>(max_charges-1)|boss&fight_remains<gcd*charges
actions.cleave+=/incinerate

actions.havoc=conflagrate,if=talent.backdraft&buff.backdraft.down&soul_shard>=1&soul_shard<=4
actions.havoc+=/soul_fire,if=cast_time<havoc_remains&soul_shard<2.5
actions.havoc+=/channel_demonfire,if=soul_shard<4.5&talent.raging_demonfire.rank=2&active_enemies>2
actions.havoc+=/immolate,cycle_targets=1,if=(dot.immolate.refreshable&dot.immolate.remains<havoc_remains|!dot.immolate.ticking)&soul_shard<4.5&target.time_to_die>8
actions.havoc+=/chaos_bolt,if=((talent.cry_havoc&!talent.inferno)|!talent.rain_of_fire)&cast_time<havoc_remains
actions.havoc+=/chaos_bolt,if=cast_time<havoc_remains&(active_enemies<=3-talent.inferno+(talent.madness_of_the_azjaqir&!talent.inferno))
actions.havoc+=/rain_of_fire,if=active_enemies>=3&talent.inferno
actions.havoc+=/rain_of_fire,if=(active_enemies>=4-talent.inferno+talent.madness_of_the_azjaqir)
actions.havoc+=/rain_of_fire,if=active_enemies>1&(talent.avatar_of_destruction|(talent.rain_of_chaos&buff.rain_of_chaos.up))&talent.inferno.enabled
actions.havoc+=/conflagrate,if=!talent.backdraft
actions.havoc+=/incinerate,if=cast_time<havoc_remains

actions.items+=/use_item,slot=trinket1,if=pet.infernal.active|!talent.summon_infernal|boss&fight_remains<21|trinket.1.cooldown.duration<cooldown.summon_infernal.remains+5
actions.items+=/use_item,slot=trinket2,if=pet.infernal.active|!talent.summon_infernal|boss&fight_remains<21|trinket.2.cooldown.duration<cooldown.summon_infernal.remains+5
actions.items+=/use_item,slot=trinket1,if=(!talent.rain_of_chaos&boss&fight_remains<cooldown.summon_infernal.remains+trinket.1.cooldown.duration&time_to_die>trinket.1.cooldown.duration)|boss&fight_remains<cooldown.summon_infernal.remains|(trinket.2.cooldown.remains>0&trinket.2.cooldown.remains<cooldown.summon_infernal.remains)
actions.items+=/use_item,slot=trinket2,if=(!talent.rain_of_chaos&boss&fight_remains<cooldown.summon_infernal.remains+trinket.2.cooldown.duration&time_to_die>trinket.2.cooldown.duration)|boss&fight_remains<cooldown.summon_infernal.remains|(trinket.1.cooldown.remains>0&trinket.1.cooldown.remains<cooldown.summon_infernal.remains)
actions.items+=/use_item,name=erupting_spear_fragment,if=(!talent.rain_of_chaos&boss&fight_remains<cooldown.summon_infernal.remains+trinket.erupting_spear_fragment.cooldown.duration&time_to_die>trinket.erupting_spear_fragment.cooldown.duration)|boss&fight_remains<cooldown.summon_infernal.remains|trinket.erupting_spear_fragment.cooldown.duration<cooldown.summon_infernal.remains+5
actions.items+=/use_item,name=desperate_invokers_codex
actions.items+=/use_item,name=iceblood_deathsnare
actions.items+=/use_item,name=conjured_chillglobe

actions.ogcd=potion,if=pet.infernal.active|!talent.summon_infernal
actions.ogcd+=/berserking,if=pet.infernal.active|!talent.summon_infernal|(boss&fight_remains<(cooldown.summon_infernal.remains+cooldown.berserking.duration)&(time_to_die>cooldown.berserking.duration))|boss&fight_remains<cooldown.summon_infernal.remains
actions.ogcd+=/blood_fury,if=pet.infernal.active|!talent.summon_infernal|(boss&fight_remains<cooldown.summon_infernal.remains+10+cooldown.blood_fury.duration&time_to_die>cooldown.blood_fury.duration)|boss&fight_remains<cooldown.summon_infernal.remains
actions.ogcd+=/fireblood,if=pet.infernal.active|!talent.summon_infernal|(boss&fight_remains<cooldown.summon_infernal.remains+10+cooldown.fireblood.duration&time_to_die>cooldown.fireblood.duration)|boss&fight_remains<cooldown.summon_infernal.remains