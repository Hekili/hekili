actions.precombat+=/power_word_fortitude

# VARIABLES
actions.main_variables+=/variable,name=chastise_cdr,op=set,value=((cooldown.divine_word.remains%action.smite.execute_time)*4)

# RUN ACTIONS
actions+=/run_action_list,name=main

# MAIN
actions.main+=/call_action_list,name=main_variables
actions.main+=/call_action_list,name=cooldowns
# Always use HF if we have empyreal blaze or harmonious apparatus  Otherwise, only use it if not ticking or refreshable
actions.main+=/holy_fire,if=(talent.empyreal_blaze|talent.harmonious_apparatus)|(!ticking|refreshable)
# Don't cast SW:P during apotheosis
actions.main+=/shadow_word_pain,if=(refreshable|!ticking)&buff.apotheosis.down
# Divine Word only if we can sync with Chastise.  If we have Empyreal Blaze, sync with that as well.
actions.main+=/divine_word,if=cooldown.holy_word_chastise.up&(!talent.empyreal_blaze|cooldown.empyreal_blaze.up)
# Holy word chastise early to trigger divine favor: chastise
actions.main+=/holy_word_chastise,if=buff.divine_word.up
# Enter Divine Favor rotation with divine favor: chastise buff up
actions.main+=/run_action_list,name=divine_favor_chastise_active,if=buff.divine_favor_chastise.up
# Run divine favor fillers rotation with buff down
actions.main+=/run_action_list,name=divine_favor_filler,if=talent.divine_word&talent.holy_word_chastise&buff.divine_favor_chastise.down
# Run divine image rotation with divine image
actions.main+=/run_action_list,name=divine_image,if=talent.divine_image
# Otherwise generic rotation
actions.main+=/run_action_list,name=generic

# Divine Favor (Active)
actions.divine_favor_chastise_active+=/holy_word_chastise
actions.divine_favor_chastise_active+=/empyreal_blaze
actions.divine_favor_chastise_active+=/apotheosis,if=cooldown.holy_word_chastise.remains>10
actions.divine_favor_chastise_active+=/shadow_word_death,if=target.health.pct<50
actions.divine_favor_chastise_active+=/mindgames
actions.divine_favor_chastise_active+=/holy_nova,if=talent.rhapsody&buff.rhapsody.stack=buff.rhapsody.max_stack&spell_targets.holy_nova>=3
actions.divine_favor_chastise_active+=/divine_star
actions.divine_favor_chastise_active+=/halo
actions.divine_favor_chastise_active+=/smite

# Divine Favor (Filler)
actions.divine_favor_filler+=/holy_word_chastise,if=(cooldown.holy_word_chastise.duration-variable.chastise_cdr)<cooldown.divine_word.remains
actions.divine_favor_filler+=/shadow_word_death,if=target.health.pct<50
actions.divine_favor_filler+=/mindgames
actions.divine_favor_filler+=/holy_nova,if=talent.rhapsody&buff.rhapsody.stack=buff.rhapsody.max_stack&spell_targets>=3
actions.divine_favor_filler+=/divine_star
actions.divine_favor_filler+=/halo
actions.divine_favor_filler+=/smite

# Divine Image
actions.divine_image+=/holy_word_sanctify
actions.divine_image+=/holy_word_serenity
actions.divine_image+=/holy_word_chastise
actions.divine_image+=/empyreal_blaze
actions.divine_image+=/apotheosis,if=cooldown.holy_word_chastise.remains>10
actions.divine_image+=/shadow_word_death,if=target.health.pct<50&!(buff.apotheosis.up|buff.answered_prayers.up)
actions.divine_image+=/mindgames,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.divine_image+=/holy_nova,if=talent.rhapsody&buff.rhapsody.stack=buff.rhapsody.max_stack&spell_targets>=3&!(buff.apotheosis.up|buff.answered_prayers.up)
actions.divine_image+=/divine_star,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.divine_image+=/halo,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.divine_image+=/smite

# Generic
actions.generic+=/holy_word_chastise
actions.generic+=/empyreal_blaze
actions.generic+=/apotheosis,if=cooldown.holy_word_chastise.remains>10
actions.generic+=/shadow_word_death,if=target.health.pct<50&!(buff.apotheosis.up|buff.answered_prayers.up)
actions.generic+=/mindgames,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.generic+=/holy_nova,if=talent.rhapsody&buff.rhapsody.stack=buff.rhapsody.max_stack&spell_targets>=3&!(buff.apotheosis.up|buff.answered_prayers.up)
actions.generic+=/divine_star,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.generic+=/halo,if=!(buff.apotheosis.up|buff.answered_prayers.up)
actions.generic+=/smite

# Cooldowns  Sync PI with divine favor: chastise if we took divine word
actions.cooldowns+=/power_infusion,if=!talent.divine_word|(talent.divine_word&buff.divine_favor_chastise.up)
# Only potion in sync with power infusion
actions.cooldowns+=/potion,if=buff.power_infusion.up|boss&cooldown.power_infusion.remains>fight_remains
# hold trinkets to use with PI
actions.cooldowns+=/use_items,if=buff.power_infusion.up|boss&cooldown.power_infusion.remains>fight_remains
actions.cooldowns+=/shadowfiend
