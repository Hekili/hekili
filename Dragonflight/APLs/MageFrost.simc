actions.precombat+=/arcane_intellect
actions.precombat+=/summon_water_elemental
actions.precombat+=/blizzard,if=active_enemies>=2
actions.precombat+=/frostbolt,if=active_enemies=1

actions+=/counterspell
actions+=/water_jet,if=cooldown.flurry.charges_fractional<1
actions+=/call_action_list,name=cds
actions+=/call_action_list,name=aoe,strict=1,if=active_enemies>=7|active_enemies>=3&talent.ice_caller
actions+=/call_action_list,name=st,strict=1,if=active_enemies<7&(active_enemies<3|!talent.ice_caller)
actions+=/call_action_list,name=movement

actions.aoe+=/cone_of_cold,if=buff.snowstorm.stack=buff.snowstorm.max_stack&debuff.frozen.up&(prev_gcd.1.frost_nova|prev_gcd.1.ice_nova|prev_off_gcd.freeze)
actions.aoe+=/frozen_orb
actions.aoe+=/blizzard
actions.aoe+=/comet_storm
actions.aoe+=/freeze,if=(target.level<level+3|target.is_add)&(!talent.snowstorm&debuff.frozen.down|cooldown.cone_of_cold.ready&buff.snowstorm.stack=buff.snowstorm.max_stack)
actions.aoe+=/ice_nova,if=(target.level<level+3|target.is_add)&(prev_gcd.1.comet_storm|cooldown.cone_of_cold.ready&buff.snowstorm.stack=buff.snowstorm.max_stack&gcd.max<1)
actions.aoe+=/frost_nova,if=(target.level<level+3|target.is_add)&active_enemies>=5&cooldown.cone_of_cold.ready&buff.snowstorm.stack=buff.snowstorm.max_stack&gcd.max<1
actions.aoe+=/cone_of_cold,if=buff.snowstorm.stack=buff.snowstorm.max_stack
actions.aoe+=/flurry,if=cooldown_react&remaining_winters_chill=0&debuff.winters_chill.down&(prev_gcd.1.frostbolt|(active_enemies>=7|charges=max_charges)&buff.fingers_of_frost.react=0)
actions.aoe+=/ice_lance,if=buff.fingers_of_frost.react|debuff.frozen.remains>travel_time|remaining_winters_chill
actions.aoe+=/shifting_power
actions.aoe+=/ice_nova
actions.aoe+=/meteor
actions.aoe+=/dragons_breath,if=active_enemies>=7
actions.aoe+=/arcane_explosion,if=mana.pct>30&active_enemies>=7
actions.aoe+=/ebonbolt
actions.aoe+=/frostbolt

actions.cds+=/time_warp,if=buff.exhaustion.up&buff.bloodlust.down
actions.cds+=/potion,if=prev_off_gcd.icy_veins|boss&fight_remains<60
actions.cds+=/icy_veins,if=buff.rune_of_power.down&(buff.icy_veins.down|talent.rune_of_power)
actions.cds+=/rune_of_power,if=buff.rune_of_power.down&cooldown.icy_veins.remains>10
actions.cds+=/use_items
## External buffs are not invoked by the addon, as the player can't choose to trigger them personally.
## actions.cds+=/invoke_external_buff,name=power_infusion,if=buff.power_infusion.down
## actions.cds+=/invoke_external_buff,name=blessing_of_summer,if=buff.blessing_of_summer.down
actions.cds+=/blood_fury
actions.cds+=/berserking
actions.cds+=/lights_judgment
actions.cds+=/fireblood
actions.cds+=/ancestral_call

actions.movement+=/blink_any,if=movement.distance>10
actions.movement+=/ice_floes,if=buff.ice_floes.down
actions.movement+=/ice_nova
actions.movement+=/arcane_explosion,if=mana.pct>30&active_enemies>=2
actions.movement+=/fire_blast
actions.movement+=/ice_lance

actions.st+=/meteor,if=prev_gcd.1.flurry
actions.st+=/comet_storm,if=prev_gcd.1.flurry
actions.st+=/flurry,if=cooldown_react&remaining_winters_chill=0&debuff.winters_chill.down&(prev_gcd.1.frostbolt|prev_gcd.1.glacial_spike)
actions.st+=/ray_of_frost,if=remaining_winters_chill=1&buff.freezing_winds.down
actions.st+=/glacial_spike,if=remaining_winters_chill
actions.st+=/cone_of_cold,if=buff.snowstorm.stack=buff.snowstorm.max_stack&remaining_winters_chill
actions.st+=/frozen_orb
actions.st+=/blizzard,if=active_enemies>=2&talent.ice_caller&talent.freezing_rain
actions.st+=/shifting_power,if=buff.rune_of_power.down
actions.st+=/ice_lance,if=buff.fingers_of_frost.react&!prev_gcd.1.glacial_spike|remaining_winters_chill
actions.st+=/ice_nova,if=active_enemies>=4
actions.st+=/glacial_spike,if=action.flurry.cooldown_react
actions.st+=/ebonbolt,if=cooldown.flurry.charges_fractional<1
actions.st+=/bag_of_tricks
actions.st+=/frostbolt